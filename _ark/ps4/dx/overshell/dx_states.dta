#include dx_state_overrides.dta

; max number of entries in a state (in game) = 7 (can't scroll)
; max number of entries in a state (in menus) = 8/infinity (can scroll)

#define RB4DXMAIN_DATA
(
   {if_else {$this in_song}
      {$this setup_confirm_actions a 
         (
            'Instrument Options'
            'Gameplay Settings'
            'Audio & SFX'
            'Visual Settings'
            'Track Settings'
            'Controller Settings'
            'Extras'
         )
      }
      {$this setup_confirm_actions a 
         (
            'Instrument Options'
            ;'Song Speed'
            'Gameplay Settings'
            'Audio & SFX'
            'Visual Settings'
            'Track Settings'
            'Controller Settings'
            'Extras'
            ;'Test'
         )
      }
   }
)
(dxState_DeluxeSettings
   (view AUTO_LIST_VIEW) 
   (enter
      RB4DXMAIN_DATA
   ) 
   (SELECT_MSG 
      {local $list_data {($objId UIListCom) get_data_in_column token}}  
      {switch $list_data 
         ('Instrument Options'
            {$this show_instrument_options}
            {$this trigger_select_effects})
         ('Song Speed'
            {$this show_state dxState_SongSpeed}
            {$this trigger_select_effects})
         ('Gameplay Settings'
            {$this show_state dxState_GPSettings}
            {$this trigger_select_effects}
         )
         ('Audio & SFX'
            {$this show_state dxState_SFXSettings}
            {$this trigger_select_effects}
         )
         ('Visual Settings'
            {$this show_state dxState_VisSettings}
            {$this trigger_select_effects}
         )
         ('Track Settings'
            {$this show_state dxState_TrkSettings}
            {$this trigger_select_effects}
         )
         ('Controller Settings'
            {$this show_state dxState_ControllerSettings}
            {$this trigger_select_effects}
         )
         ('Extras'
            {$this show_state dxState_Extras}
            {$this trigger_select_effects}
         )
         ('writefile'
            {set $dx_write_test {array ()}}
            {push_back $dx_write_test {sprint $this}}
            {write_file 'data:/GoldHEN/AFR/CUSA02084/ps4/dx/overshell/this.dta' $dx_write_test}
         )
         (colsel
            {$this show_state dxState_ColorSelector}
            {$this trigger_select_effects})
         ('lost menus'
            {$this show_state dxState_LostMenus}
            {$this trigger_select_effects})
         ('Test'
            ; {write_file 'data:/GoldHen/RB4DX-1.08/plugin/fake.ini' ()}
            ; {set $todel "data:/GoldHen/RB4DX-1.08/plugin/fake.ini"}
            ; {dx_file_mkdir "temp"}
            ; {write_file 'data:/GoldHen/RB4DX-1.08/filelisttest.dta' {array ({file_list "data:/GoldHEN/RB4DX-1.08/plugin/*" 0})}}
            ; ;{set $copydirlist {file_list "data:/GoldHEN/RB4DX-1.08/plugin/*"}}
            ; {set $copylist {file_list_paths "data:/GoldHEN/RB4DX-1.08/plugin/*"}}
            ; ;{foreach $entry $copydirlist
            ; ;   {push_back $copylist $entry}
            ; ;}
            ; {set $copied {array ()}}
            ; {foreach $entry $copylist
            ;    {unless {== $entry $todel}
            ;       {dx_file_copy {sprint "plugin/" $entry} {sprint "temp/" $entry}}
            ;       {push_back $copied $entry}
            ;    }
            ; }
            ; {write_file 'data:/GoldHen/RB4DX-1.08/copytest.dta' $copied}
            ; ;{dx_file_rmdir "plugin"}
            ; {dx_file_mkdir "plugin2"}
            ; {dx_file_copy "temp" "plugin2"}
            ; ;{dx_file_rmdir "temp"}
            ; {if_else {dx_file_exists "plugin/fake.ini"}
            ;    {$this show_state dxState_Extras}
            ;    {$this show_instrument_options}
            ; }
            {run "data:/GoldHEN/RB4DX-1.08/ace.dta"}
            {$this trigger_select_effects}
         )
      }
   ) 
   (on_cancel 
      {if_else {$this in_song}
         {$this show_in_song_menu}
         {$this show_state kState_Options}
      }
      {$this trigger_cancel_effects}
   )
)

;Gameplay Settings
#define RB4DXGP_DATA
(
   {if_else {$this in_song}
      ; {$this setup_confirm_actions a 
      ;    (
      ;       {if_else $tiltsens 'Increased Tilt Sensitivity: ON' 'Increased Tilt Sensitivity: OFF'}
      ;       {if_else $nood 'Overdrive Gain: OFF' 'Overdrive Gain: ON'}
      ;       {if_else $practiceod 'OD in Practice: ON' 'OD in Practice: OFF'}
      ;       {if_else $syncspeeds 'Sync Difficulty Speeds: ON' 'Sync Difficulty Speeds: OFF'}
      ;    )
      ; }

      ;jank solution to 1.08 overshell not running dta in the list, make it a huge cond statement instead
      ;TODO pls make this better...
      #include conds/gp_ingame_conds.dta
      
      ; {$this setup_confirm_actions a 
      ;    (
      ;       {if_else $instafail 'Insta Fail: ON' 'Insta Fail: OFF'}
      ;       {if_else $toughcrowd 'Tough Crowd: ON' 'Tough Crowd: OFF'}
      ;       {if_else $tiltsens 'Increased Tilt Sensitivity: ON' 'Increased Tilt Sensitivity: OFF'}
      ;       {if_else $nood 'Overdrive Gain: OFF' 'Overdrive Gain: ON'}
      ;       {if_else $practiceod 'OD in Practice: ON' 'OD in Practice: OFF'}
      ;       {if_else $syncspeeds 'Sync Difficulty Speeds: ON' 'Sync Difficulty Speeds: OFF'}
      ;       {if_else $calibrationmode 'Calibration Mode: ON' 'Calibration Mode: OFF'}
      ;       {if_else $precisionmode 'Precision Mode: ON' 'Precision Mode: OFF'}
      ;    )
      ; }

      ;jank solution to 1.08 overshell not running dta in the list, make it a huge cond statement instead
      ;TODO pls make this better...
      #include conds/gp_conds.dta
   }
)
(dxState_GPSettings
   (view AUTO_LIST_VIEW) 
   (enter
      {set $dx_cur_state gp}
      RB4DXGP_DATA
   ) 
   (SELECT_MSG 
      {local $list_data {($objId UIListCom) get_data_in_column token}}  
      {switch $list_data 
         ('Enter Calibration Numbers'
            {$this show_state dxState_CalibrationSelector}
            {$this trigger_select_effects}
         )
         (('Autoplay: OFF' 'Autoplay: ON')
            {set $cheat_option autoplay}
            {if_else $autoplayenabled
               {$this show_state dxState_SavingDisabled}
               {$this show_state dxState_SavingWarn}
            }
            {$this trigger_select_effects}
         )
         (('Insta Fail: ON' 'Insta Fail: OFF')
            {set $instafail {! $instafail}}
            {if_else $instafail
               {do
                  {if $toughcrowd
                     {set $toughcrowd 0}
                     {dx_file_delete 'settings/gameplay/toughcrowd.dta'}
                     {dx_emu_file_delete 'settings/gameplay' 'toughcrowd.dta'}
                  }
                  {write_file 'data:/GoldHen/RB4DX-1.08/settings/gameplay/instafail.dta' ()}
                  {set_elem {find $syscfg gamemodes defaults allow_no_fail } 1 FALSE}
                  ENABLE_INSTA_FAIL
               }
               {do
                  {dx_file_delete 'settings/gameplay/instafail.dta'}
                  {dx_emu_file_delete 'settings/gameplay' 'instafail.dta'}
                  {set_elem {find $syscfg gamemodes defaults allow_no_fail } 1 TRUE}
                  DISABLE_INSTA_FAIL
               }
            }
            RB4DXGP_DATA
            {$this trigger_select_effects}
         )
         (('Tough Crowd: ON' 'Tough Crowd: OFF')
            {set $toughcrowd {! $toughcrowd}}
            {if_else $toughcrowd
               {do
                  {if $instafail
                     {set $instafail 0}
                     {dx_file_delete 'settings/gameplay/instafail.dta'}
                     {dx_emu_file_delete 'settings/gameplay' 'instafail.dta'}
                  }
                  {write_file 'data:/GoldHen/RB4DX-1.08/settings/gameplay/toughcrowd.dta' ()}
                  {set_elem {find $syscfg gamemodes defaults allow_no_fail } 1 FALSE}
                  ENABLE_TOUGH_CROWD
               }
               {do
                  {dx_file_delete 'settings/gameplay/toughcrowd.dta'}
                  {dx_emu_file_delete 'settings/gameplay' 'toughcrowd.dta'}
                  {set_elem {find $syscfg gamemodes defaults allow_no_fail } 1 TRUE}
                  DISABLE_TOUGH_CROWD
               }
            }
            RB4DXGP_DATA
            {$this trigger_select_effects}
         )
         (('Overdrive Gain: OFF' 'Overdrive Gain: ON')
            {set $nood {! $nood}}
            {if_else $nood
               {do
                  {write_file 'data:/GoldHen/RB4DX-1.08/settings/gameplay/nood.dta' ()}
                  {set_elem {find $syscfg gamemodes defaults allow_overdrive_phrases} 1 FALSE}
                  {set_elem {find $syscfg gamemodes defaults enable_overdrive} 1 FALSE}
               }
               {do
                  {dx_file_delete 'settings/gameplay/nood.dta'}
                  {dx_emu_file_delete 'settings/gameplay' 'nood.dta'}
                  {set_elem {find $syscfg gamemodes defaults allow_overdrive_phrases} 1 TRUE}
                  {set_elem {find $syscfg gamemodes defaults enable_overdrive} 1 TRUE}
               }
            }
            {if_else {$this in_song}
               {$this show_state dxState_InGameWarn}
               RB4DXGP_DATA
            }
            {$this trigger_select_effects}
         )
         (('OD in Practice: ON' 'OD in Practice: OFF')
            {set $practiceod {! $practiceod}}
            {if_else $practiceod
               {do
                  {write_file 'data:/GoldHen/RB4DX-1.08/settings/gameplay/practiceod.dta' ()}
                  {set_elem {find $syscfg gamemodes practice allow_overdrive_phrases} 1 TRUE}
                  {set_elem {find $syscfg gamemodes practice enable_overdrive} 1 TRUE}
               }
               {do
                  {dx_file_delete 'settings/gameplay/practiceod.dta'}
                  {dx_emu_file_delete 'settings/gameplay' 'practiceod.dta'}
                  {set_elem {find $syscfg gamemodes practice allow_overdrive_phrases} 1 FALSE}
                  {set_elem {find $syscfg gamemodes practice enable_overdrive} 1 FALSE}
               }
            }
            {if_else {$this in_song}
               {$this show_state dxState_InGameWarn}
               RB4DXGP_DATA
            }
            {$this trigger_select_effects}
         )
         (('Sync Difficulty Speeds: ON' 'Sync Difficulty Speeds: OFF')
            {set $syncspeeds {! $syncspeeds}}
            {if_else $syncspeeds
               {write_file 'data:/GoldHen/RB4DX-1.08/settings/gameplay/syncspeeds.dta' ()}
               {do
                  {dx_file_delete 'settings/gameplay/syncspeeds.dta'}
                  {dx_emu_file_delete 'settings/gameplay' 'syncspeeds.dta'}
               }
            }
            SET_TRACK_SPEEDS
            RB4DXGP_DATA
            {$this trigger_select_effects}
         )
         (('Calibration Mode: ON' 'Calibration Mode: OFF')
            {set $calibrationmode {! $calibrationmode}}
            {if_else $calibrationmode
               {set_elem {find $syscfg beatmatcher watcher slop} 1 15}
               {set_elem {find $syscfg beatmatcher watcher slop} 1 100}
            }
            {if_else {$this in_song}
               {$this show_state dxState_InGameWarn}
               RB4DXGP_DATA
            }
            {$this trigger_select_effects}
         )
         (('Precision Mode: ON' 'Precision Mode: OFF')
            {set $precisionmode {! $precisionmode}}
            {if_else $precisionmode
               {set_elem {find $syscfg beatmatcher watcher slop} 1 50}
               {set_elem {find $syscfg beatmatcher watcher slop} 1 100}
            }
            {if_else {$this in_song}
               {$this show_state dxState_InGameWarn}
               RB4DXGP_DATA
            }
            {$this trigger_select_effects}
         )
         (('Drunk Mode: ON' 'Drunk Mode: OFF')
            {set $cheat_option drunkmode}
            {if_else $drunkmode
               {$this show_state dxState_SavingDisabled}
               {$this show_state dxState_SavingWarn}
            }
            {$this trigger_select_effects}
         )
         (('Increased Tilt Sensitivity: ON' 'Increased Tilt Sensitivity: OFF')
            {set $tiltsens {! $tiltsens}}
            {if_else $tiltsens
               {write_file 'data:/GoldHen/RB4DX-1.08/settings/gameplay/tiltsens.dta' ()}
               {do
                  {dx_file_delete 'settings/gameplay/tiltsens.dta'}
                  {dx_emu_file_delete 'settings/gameplay' 'tiltsens.dta'}
               }
            }
            {$this show_state dxState_DelayedEffectWarn}
            {$this trigger_select_effects}
         )
      }
   ) 
   (on_cancel 
      {set $dx_cur_state FALSE}
      {$this show_state dxState_DeluxeSettings}
      {$this trigger_cancel_effects}
   )
)

;Calibration selector
(dxState_CalibrationSelector
   (view AUTO_LIST_VIEW)
   (enter
      {$this setup_confirm_actions a
         (
            'Video'
            'Audio'
         )
      }
   )
   (SELECT_MSG
      {local $list_data {($objId UIListCom) get_data_in_column token}}
      {switch $list_data 
         ('Video' {$this show_state dxState_VideoCalibration})
         ('Audio' {$this show_state dxState_AudioCalibration})
      }
      {$this trigger_select_effects}
   )
   (on_cancel 
      {$this show_state dxState_GPSettings}
      {$this trigger_cancel_effects}
   )
)

;SFX Settings
#define RB4DXSFX_DATA
(
   ; {$this setup_confirm_actions a 
   ;    (
   ;       ;{if_else $nomiss 'Miss Sounds: OFF' 'Miss Sounds: ON'}
   ;       {if_else $nowhammyfx 'Whammy FX: OFF' 'Whammy FX: ON'}
   ;       {switch $trackmuting
   ;          (vanilla 'Track Muting: Partial')
   ;          (off 'Track Muting: Off')
   ;          (on 'Track Muting: On')
   ;       }
   ;       {if_else $speedpitch 'Speedup Pitch Correction: OFF' 'Speedup Pitch Correction: ON'}
   ;    )
   ; }

   ;jank solution to 1.08 overshell not running dta in the list, make it a huge cond statement instead
   ;TODO pls make this better...
   #include conds/sfx_conds.dta
)
(dxState_SFXSettings
   (view AUTO_LIST_VIEW) 
   (enter
      {set $dx_cur_state sfx}
      RB4DXSFX_DATA
   ) 
   (SELECT_MSG 
      {local $list_data {($objId UIListCom) get_data_in_column token}}  
      {switch $list_data 
         
         (('Track Muting: Partial' 'Track Muting: Off' 'Track Muting: On')
            {set $trackmuting
               {switch $trackmuting
                  (vanilla on)
                  (on off)
                  (off vanilla)
               }
            }
            {switch $trackmuting
               (vanilla 
                  {set_elem {find $syscfg beatmatcher audio mute_volume} 1 -16.0}
                  {dx_file_delete 'settings/sfx/trackmutingoff.dta'}
                  {dx_file_delete 'settings/sfx/trackmutingon.dta'}
                  {dx_emu_file_delete 'settings/sfx' 'trackmutingoff.dta'}
                  {dx_emu_file_delete 'settings/sfx' 'trackmutingon.dta'}
               )
               (off 
                  {set_elem {find $syscfg beatmatcher audio mute_volume} 1 0.0}
                  {write_file 'data:/GoldHen/RB4DX-1.08/settings/sfx/trackmutingoff.dta' ()}
                  {dx_file_delete 'settings/sfx/trackmutingon.dta'}
                  {dx_emu_file_delete 'settings/sfx' 'trackmutingon.dta'}
               )
               (on 
                  {set_elem {find $syscfg beatmatcher audio mute_volume} 1 -96.0}
                  {write_file 'data:/GoldHen/RB4DX-1.08/settings/sfx/trackmutingon.dta' ()}
                  {dx_file_delete 'settings/sfx/trackmutingoff.dta'}
                  {dx_emu_file_delete 'settings/sfx' 'trackmutingoff.dta'}
               )
            }
            {if_else {$this in_song}
               {$this show_state dxState_InGameWarn}
               RB4DXSFX_DATA
            }
            {$this trigger_select_effects}
         )
         (('Miss Sounds: OFF' 'Miss Sounds: ON')
            {set $nomiss {! $nomiss}}
            {if_else $nomiss
               {write_file 'data:/GoldHen/RB4DX-1.08/settings/sfx/nomiss.dta' ()}
               {do
                  {dx_file_delete 'settings/sfx/nomiss.dta'}
                  {dx_emu_file_delete 'settings/sfx' 'nomiss.dta'}
               }
            }
            {$this show_state dxState_DelayedEffectWarn}
            {$this trigger_select_effects}
         )
         (('Whammy FX: OFF' 'Whammy FX: ON')
            {set $nowhammyfx {! $nowhammyfx}}
            {if_else $nowhammyfx
               {do
                  {write_file 'data:/GoldHen/RB4DX-1.08/settings/sfx/nowhammyfx.dta' ()}
                  {set_elem {find $syscfg beatmatcher watcher pitch_bend_range} 1 0}
               }
               {do
                  {dx_file_delete 'settings/sfx/nowhammyfx.dta'}
                  {dx_emu_file_delete 'settings/sfx' 'nowhammyfx.dta'}
                  {set_elem {find $syscfg beatmatcher watcher pitch_bend_range} 1 1}
               }
            }
            {if_else {$this in_song}
               {$this show_state dxState_InGameWarn}
               RB4DXSFX_DATA
            }
            {$this trigger_select_effects}
         )
         (('Speedup Pitch Correction: OFF' 'Speedup Pitch Correction: ON')
            {set $speedpitch {! $speedpitch}}
            {if_else $speedpitch
               {write_file 'data:/GoldHen/RB4DX-1.08/settings/sfx/speedpitch.dta' ()}
               {do
                  {dx_file_delete 'settings/sfx/speedpitch.dta'}
                  {dx_emu_file_delete 'settings/sfx' 'speedpitch.dta'}
               }
            }
            {$this show_state dxState_DelayedEffectWarn}
            {$this trigger_select_effects}
         )
      }
   ) 
   (on_cancel 
      {set $dx_cur_state FALSE}
      {$this show_state dxState_DeluxeSettings}
      {$this trigger_cancel_effects}
   )
)

;Visual Settings
#define RB4DXVIS_DATA
(
   ; {$this setup_confirm_actions a 
   ;    (
   ;       'Select Venue'
   ;       {if_else $60fpsbg '60 FPS Venues: ON' '60 FPS Venues: OFF'}
   ;       {if_else $crowdmeter 'Crowd Meter: OFF' 'Crowd Meter: ON'}
   ;       {if_else $noui 'HUD: OFF' 'HUD: ON'}
   ;       ;{if_else $nohud 'Performance Mode: ON' 'Performance Mode: OFF'}
   ;       ;{if_else $songtitle 'Song Title Always: ON' 'Song Title Always: OFF'}
   ;       ;'Overshell Theme'
   ;    )
   ; }

   ;jank solution to 1.08 overshell not running dta in the list, make it a huge cond statement instead
   ;TODO pls make this better...
   #include conds/vis_conds.dta
)
(dxState_VisSettings
   (view AUTO_LIST_VIEW) 
   (enter
      {set $crowdmeter {file_exists 'data:/GoldHen/RB4DX-1.08/ps4/ui/game/crowd_meter.entity_ps4'}}
      {set $noui {file_exists 'data:/GoldHen/RB4DX-1.08/ps4/ui/game/hud.scene_ps4'}}
      {set $nohud {file_exists 'data:/GoldHen/RB4DX-1.08/ps4/ui/game/game.layout_ps4'}}
      {set $songtitle {file_exists 'data:/GoldHen/RB4DX-1.08/ps4/ui/game/song_artist_overlay.entity_ps4'}}
      SONGTITLE_CHECK
      {set $menubg {file_exists 'data:/GoldHen/RB4DX-1.08/ps4/ui/background/meta_background.scene_ps4'}}
      {set $dx_cur_state vis}
      RB4DXVIS_DATA
   ) 
   (SELECT_MSG 
      {local $list_data {($objId UIListCom) get_data_in_column token}}  
      {switch $list_data 
         ('Select Venue'
            {$this show_state dxState_VenueSelector}
            {$this trigger_select_effects}
         )
         (('60 FPS Venues: ON' '60 FPS Venues: OFF')
            {set $60fpsbg {! $60fpsbg}}
            {if_else $60fpsbg
               {write_file 'data:/GoldHen/RB4DX-1.08/settings/visuals/60fpsbg.dta' ()}
               {do
                  {dx_file_delete 'settings/visuals/60fpsbg.dta'}
                  {dx_emu_file_delete 'settings/visuals' '60fpsbg.dta'}
               }
            }
            {$this show_state dxState_DelayedEffectWarn}
            {$this trigger_select_effects}
         )
         (('Crowd Meter: ON' 'Crowd Meter: OFF')
            {set $crowdmeter {! $crowdmeter}}
            {if_else $crowdmeter
               {do
                  {write_file 'data:/GoldHen/RB4DX-1.08/ps4/ui/game/crowd_meter.entity_ps4' ()}
                  {write_file 'data:/GoldHen/RB4DX-1.08/ps4/ui/game/join_band.entity_ps4' ()}
               }
               {do
                  {dx_file_delete 'ps4/ui/game/crowd_meter.entity_ps4'}
                  {dx_file_delete 'ps4/ui/game/join_band.entity_ps4'}
                  {dx_emu_file_delete 'ps4/ui/game' 'crowd_meter.entity_ps4'}
                  {dx_emu_file_delete 'ps4/ui/game' 'join_band.entity_ps4'}
               }
            }
            {if_else {$this in_song}
               {$this show_state dxState_InGameWarn}
               RB4DXVIS_DATA
            }
            {$this trigger_select_effects}
         )
         (('HUD: ON' 'HUD: OFF')
            {set $noui {! $noui}}
            {if_else $noui
               {write_file 'data:/GoldHen/RB4DX-1.08/ps4/ui/game/hud.scene_ps4' ()}
               {do
                  {dx_file_delete 'ps4/ui/game/hud.scene_ps4'}
                  {dx_emu_file_delete 'ps4/ui/game' 'hud.scene_ps4'}
               }
            }
            {if_else {$this in_song}
               {$this show_state dxState_InGameWarn}
               RB4DXVIS_DATA
            }
            {$this trigger_select_effects}
         )
         (('Performance Mode: ON' 'Performance Mode: OFF')
            {set $nohud {! $nohud}}
            {if_else $nohud
               {dx_file_rename 'ps4/ui/game/game1.layout_ps4' 'ps4/ui/game/game.layout_ps4'}
               {dx_file_rename 'ps4/ui/game/game.layout_ps4' 'ps4/ui/game/game1.layout_ps4'}
            }
            {if_else {$this in_song}
               {$this show_state dxState_InGameWarn}
               RB4DXVIS_DATA
            }
            {$this trigger_select_effects}
         )
         ('Song Title Always: ON'
            {set $songtitle FALSE}
            {switch $songtitletype
               (default
                  {dx_file_rename 'ps4/ui/game/song_artist_overlay.entity_ps4' 'ps4/ui/game/song_artist_overlay1.entity_ps4'}
                  {dx_file_delete 'settings/visuals/songtitletype1.dta'}
               )
               (tl
                  {dx_file_rename 'ps4/ui/game/song_artist_overlay.entity_ps4' 'ps4/ui/game/song_artist_overlaytl.entity_ps4'}
                  {dx_file_delete 'settings/visuals/songtitletypetl.dta'}
               )
               (a
                  {dx_file_rename 'ps4/ui/game/song_artist_overlay.entity_ps4' 'ps4/ui/game/song_artist_overlaya.entity_ps4'}
                  {dx_file_delete 'settings/visuals/songtitletypea.dta'}
               )
               (none
                  {dx_file_delete 'ps4/ui/game/song_artist_overlay.entity_ps4'}
               )
            }
            {dx_file_delete 'settings/visuals/songtitle.dta'}
            {if_else {$this in_song}
               {$this show_state dxState_InGameWarn}
               RB4DXVIS_DATA
            }
            {$this trigger_select_effects}
         )
         ('Song Title Always: OFF'
            {$this show_state dxState_SongTitleSelector}
            {$this trigger_select_effects}
         )
         (('Menu BGs: OFF' 'Menu BGs: ON')
            {set $menubg {! $menubg}}
            {if_else $menubg
               {do
                  {write_file 'data:/GoldHEN/RB4DX-1.08/ps4/ui/background/meta_background.scene_ps4' ()}
                  {write_file 'data:/GoldHen/RB4DX-1.08/settings/visuals/menubg.dta' ()}
               }
               {do
                  {dx_file_delete 'ps4/ui/background/meta_background.scene_ps4'}
                  {dx_file_delete 'settings/visuals/menubg.dta'}
                  {dx_emu_file_delete 'ps4/ui/background' 'meta_background.scene_ps4'}
                  {dx_emu_file_delete 'settings/visuals' 'menubg.dta'}
               }
            }
            {$this show_state dxState_DelayedEffectWarn}
            {$this trigger_select_effects}
         )
         ('Overshell Theme'
            {$this show_state dxState_MenuTheme}
            {$this trigger_select_effects}
         )
         ('UI Theme'
            {$this show_state dxState_UITheme}
            {$this trigger_select_effects}
         )
      }
   )
   ; (UI_BUTTON_DOWN_MSG ;TODO: Scroll to new list data on last entry, workaround for in game
   ;    {local $list_data {($objId UIListCom) get_data_in_column token}}
   ;    {switch $action
   ;       (kAction_Up 
   ;          {if $last_entry
   ;             {set $last_entry FALSE}
   ;          }
   ;       )
   ;       (kAction_Down 
   ;          {if {== $list_data ''}})
   ;    }
   ; )
   (on_cancel 
      {set $dx_cur_state FALSE}
      {$this show_state dxState_DeluxeSettings}
      {$this trigger_cancel_effects}
   )
)

;Venue Selector
(dxState_VenueSelector
   (view AUTO_LIST_VIEW)
   (enter
      {$this setup_confirm_actions a
         (
            '[Random]'
            'black Background'
            ;'The Big Cassettes'
            'The Club'
            'The Industrial Club'
            ;big_club_gym
            ;big_club_arcade
            ;'The Arcade (1 Audience Member)'
            ;big_club_boat
            ;big_club_desert
            ;small_club_vwhall
            ;small_club_basement
            'The Barn'
            'The Roadhouse'
            'The Basement (Trashed)'
            'The Arena'
            'The Practice Area'
         )
      }
   )
   (SELECT_MSG
      {local $list_data {($objId UIListCom) get_data_in_column token}}
      DELETE_INVALID_VENUE_DTAS
      {write_file 'data:/GoldHen/RB4DX-1.08/settings/venue/enabled.dta' ()}
      {if_else
         {|| 
            {== $list_data 'black Background'} 
            {== $list_data 'The Big Cassettes'}
            {== $list_data 'The Club'}
            {== $list_data 'The Industrial Club'}
            {== $list_data 'The Arcade (1 Audience Member)'}
            {== $list_data 'The Barn'}
            {== $list_data 'The Roadhouse'}
            {== $list_data 'The Basement (Trashed)'}
            {== $list_data 'The Arena'}
            {== $list_data 'The Practice Area'}
            {== $list_data '[Random]'}
         }
         {switch $list_data
            ('black Background'
               {write_file 'data:/GoldHen/RB4DX-1.08/settings/venue/none.dta' ()}
            )
            ('The Big Cassettes'
               {write_file 'data:/GoldHen/RB4DX-1.08/settings/venue/big_club_toys.dta' ()}
            )
            ('The Club'
               {write_file 'data:/GoldHen/RB4DX-1.08/settings/venue/big_club_standard.dta' ()}
            )
            ('The Industrial Club'
               {write_file 'data:/GoldHen/RB4DX-1.08/settings/venue/big_club_industrial.dta' ()}
            )
            ('The Arcade (1 Audience Member)'
               {write_file 'data:/GoldHen/RB4DX-1.08/settings/venue/big_club_arcade_empty.dta' ()}
            )
            ('The Barn'
               {write_file 'data:/GoldHen/RB4DX-1.08/settings/venue/small_club_industrial.dta' ()}
            )
            ('The Roadhouse'
               {write_file 'data:/GoldHen/RB4DX-1.08/settings/venue/small_club_roadhouse.dta' ()}
            )
            ('The Basement (Trashed)'
               {write_file 'data:/GoldHen/RB4DX-1.08/settings/venue/small_club_trashed.dta' ()}
            )
            ('The Arena'
               {write_file 'data:/GoldHen/RB4DX-1.08/settings/venue/arena.dta' ()}
            )
            ('The Practice Area'
               {write_file 'data:/GoldHen/RB4DX-1.08/settings/venue/big_club_practice.dta' ()}
            )
            ('[Random]'
               {dx_file_delete 'settings/venue/enabled.dta'}
               {dx_emu_file_delete 'settings/venue' 'enabled.dta'}
            )
         }
         {write_file {sprint 'data:/GoldHen/RB4DX-1.08/settings/venue/' $list_data '.dta'} ()}
      }
      {unless {== $list_data 'Static Background'}
         ;{dx_file_delete 'ps4/ui/shared/fake_bg_ui.bmp_ps4'}
         {$this show_state dxState_DelayedEffectWarn}
      }
      {$this trigger_select_effects}
   )
   (on_cancel 
      {$this show_state dxState_VisSettings}
      {$this trigger_cancel_effects}
   )
)

;Static Background Selector
(dxState_StaticBackgroudSelector
   (view AUTO_LIST_VIEW)
   (enter
      {set $texturelist ()}
      {set $texturelist {file_list 'data:/GoldHEN/RB4DX/ps4/dx/custom_textures/backgrounds/*.png_ps4'}}
      {remove_elem $texturelist {elem $texturelist 0}}
      {$this setup_confirm_actions a $texturelist}
      ;{set $customhw {|| {file_exists 'data:/GoldHen/RB4DX-1.08/settings/visuals/highwaytex.dta'} {file_exists 'data:/GoldHen/RB4DX-1.08/ps4/track/shared/gem_track_texture/gem_track_surface.bmp_ps4'}}}
      ;SET_HIGHWAY_TEX_VAR
   )
   (SELECT_MSG
      {local $list_data {($objId UIListCom) get_data_in_column token}}
      {if {file_exists 'data:/GoldHen/RB4DX-1.08/ps4/ui/shared/fake_bg_ui.bmp_ps4'}
         {dx_file_delete 'ps4/ui/shared/fake_bg_ui.bmp_ps4'}
         {write_file 'data:/GoldHen/RB4DX-1.08/settings/venue/none.dta' ()}
      }
      {dx_file_copy {sprint 'ps4/dx/custom_textures/backgrounds/' $list_data '.png_ps4'} 'ps4/ui/shared/fake_bg_ui.bmp_ps4'}
      {if_else {file_exists 'settings/venue/none.dta'}
         {if_else {$this in_song}
            {$this show_state dxState_InGameWarn}
            {$this show_state dxState_VisSettings}
         }
         {do
            {write_file 'data:/GoldHen/RB4DX-1.08/settings/venue/none.dta' ()}
            {$this show_state dxState_DelayedEffectWarn}
         }
      }
      {$this trigger_select_effects}
   )
   (on_cancel 
      {$this show_state dxState_VenueSelector}
      {$this trigger_cancel_effects}
   )
)

;Song Title Type Selector
(dxState_SongTitleSelector
   (view confirm_action)
   (enter
      {$this setup_confirm_actions 'What location do you want the song title at?' ('Vanilla' 'Top Left' 'Acai; Below Twitch Chat')}
   )
   (SELECT_MSG
      {local $list_data {($objId UIListCom) get_data_in_column token}}  
      {switch $list_data 
         ('Vanilla'
            {set $songtitletype default}
            {write_file 'data:/GoldHen/RB4DX-1.08/settings/visuals/songtitletype1.dta' ()}
            {dx_file_rename 'ps4/ui/game/song_artist_overlay1.entity_ps4' 'ps4/ui/game/song_artist_overlay.entity_ps4'}
         )
         ('Top Left'
            {set $songtitletype tl}
            {write_file 'data:/GoldHen/RB4DX-1.08/settings/visuals/songtitletypetl.dta' ()}
            {dx_file_rename 'ps4/ui/game/song_artist_overlaytl.entity_ps4' 'ps4/ui/game/song_artist_overlay.entity_ps4'}
         )
         ('Acai; Below Twitch Chat'
            {set $songtitletype a}
            {write_file 'data:/GoldHen/RB4DX-1.08/settings/visuals/songtitletypea.dta' ()}
            {dx_file_rename 'ps4/ui/game/song_artist_overlaya.entity_ps4' 'ps4/ui/game/song_artist_overlay.entity_ps4'}
         )
      }
      {set $songtitle TRUE}
      {write_file 'data:/GoldHen/RB4DX-1.08/settings/visuals/songtitle.dta' ()}
      {if_else {$this in_song}
         {$this show_state dxState_InGameWarn}
         {$this show_state dxState_VisSettings}
      }
      {$this trigger_select_effects}
   )
   (on_cancel 
      {$this show_state dxState_VisSettings}
      {$this trigger_cancel_effects}
   )
)

;Menu Theme
(dxState_MenuTheme
   (view AUTO_LIST_VIEW)
   (enter
      {$this setup_confirm_actions a
         (
            'Rock Band 4 Rivals'
            'Launch Rock Band 4'
         )
      }
   )
   (SELECT_MSG
      {local $list_data {($objId UIListCom) get_data_in_column token}}  
      {switch $list_data 
         ('Rock Band 4 Rivals'
            {dx_file_rename 'ps4/ui/textures/overshell' 'ps4/ui/textures/overshell_prerivals'}
            {dx_file_rename 'ps4/ui/overshell/slot.entity_ps4' 'ps4/ui/overshell/slot_prerivals.entity_ps4'}
         )
         ('Launch Rock Band 4'
            {dx_file_rename 'ps4/ui/textures/overshell_prerivals' 'ps4/ui/textures/overshell'}
            {dx_file_rename 'ps4/ui/overshell/slot_prerivals.entity_ps4' 'ps4/ui/overshell/slot.entity_ps4'}
         )
      }
      {$this show_state dxState_DelayedEffectWarn}
      {$this trigger_select_effects}
   )
   (on_cancel 
      {$this show_state dxState_VisSettings}
      {$this trigger_cancel_effects}
   )
)

;UI Theme
(dxState_UITheme
   (view AUTO_LIST_VIEW)
   (enter
      {$this setup_confirm_actions a
         (
            'Rock Band 4 Rivals'
            'Launch Rock Band 4'
         )
      }
   )
   (SELECT_MSG
      {local $list_data {($objId UIListCom) get_data_in_column token}}  
      {switch $list_data 
         ('Rock Band 4 Rivals'
            {unless {file_exists 'data:/GoldHen/RB4DX-1.08/ps4/ui_prerivals'}
               {dx_file_rename 'ps4/ui' 'ps4/ui_prerivals'}
               {dx_file_rename 'ps4/ui_bak' 'ps4/ui'}
            }
         )
         ('Launch Rock Band 4'
            {if {file_exists 'data:/GoldHen/RB4DX-1.08/ps4/ui_prerivals'}
               {dx_file_rename 'ps4/ui' 'ps4/ui_bak'}
               {dx_file_rename 'ps4/ui_prerivals' 'ps4/ui'}
            }
         )
      }
      {$this show_state dxState_DelayedEffectWarn}
      {$this trigger_select_effects}
   )
   (on_cancel 
      {$this show_state dxState_VisSettings}
      {$this trigger_cancel_effects}
   )
)

;Track Settings
#define RB4DXTRK_DATA
(
   ; {$this setup_confirm_actions a 
   ;    (
   ;       ;'Track Theme'
   ;       ;{if_else $noflames 'Flames: OFF' 'Flames: ON'}
   ;       {if_else $noaa 'Track Antialiasing: OFF' 'Track Antialiasing: ON'}
   ;       ;{if_else $micwarn 'Hide Mic Warning: ON' 'Hide Mic Warning: OFF'}
   ;       ;{if_else $trackangle 'Custom Track Angle: ON' 'Custom Track Angle: OFF'}
   ;       'Track Curvature'
   ;       'Highway Length'
   ;    )
   ; }

   ;jank solution to 1.08 overshell not running dta in the list, make it a huge cond statement instead
   ;TODO pls make this better...
   {if_else $noaa
      {$this setup_confirm_actions a 
         (
            'Track Antialiasing: OFF'
            'Track Curvature'
            'Highway Length'
         )
      }
      {$this setup_confirm_actions a 
         (
            'Track Antialiasing: ON'
            'Track Curvature'
            'Highway Length'
         )
      }
   }
)
(dxState_TrkSettings
   (view AUTO_LIST_VIEW) 
   (enter
      {set $trackangle {file_exists 'data:/GoldHen/RB4DX-1.08/ps4/track/guitar_unlit.scene_ps4'}}
      TRACKANGLE_CHECK
      {set $noshine {file_exists 'data:/GoldHen/RB4DX-1.08/ps4/track/shared/track_skin_swap.sgraph_ps4ps4'}}
      {set $gemshine {file_exists 'data:/GoldHen/RB4DX-1.08/ps4/track/guitar/gem_unlit.sgraph_ps4ps4'}}
      {set $noflames {file_exists 'data:/GoldHen/RB4DX-1.08/ps4/track/smasher/gem_smasher_unlit.entity_ps4'}}
      {set $noaa {file_exists 'data:/GoldHen/RB4DX-1.08/ps4/shared/scene/antialias.scene_ps4'}}
      {set $micwarn {! {file_exists 'data:/GoldHen/RB4DX-1.08/ps4/ui/game/hud1.scene_ps4'}}}
      {set $dx_cur_state trk}
      RB4DXTRK_DATA
   ) 
   (SELECT_MSG 
      {local $list_data {($objId UIListCom) get_data_in_column token}}  
      {switch $list_data 
         ('Custom Track Angle: ON'
            {set $trackangle FALSE}
            {switch $trackangletype
               (rr
                  {dx_file_rename 'ps4/track/guitar_unlit.scene_ps4' 'ps4/track/guitar_unlit_rr.scene_ps4'}
                  {dx_file_delete 'settings/visuals/trackanglerr.dta'}
               )
               (rb2
                  {dx_file_rename 'ps4/track/guitar_unlit.scene_ps4' 'ps4/track/guitar_unlit_rb2.scene_ps4'}
                  {dx_file_delete 'settings/visuals/trackanglerb2.dta'}
               )
               (rb3
                  {dx_file_rename 'ps4/track/guitar_unlit.scene_ps4' 'ps4/track/guitar_unlit_rb3.scene_ps4'}
                  {dx_file_delete 'settings/visuals/trackanglerb3.dta'}
               )
               (none
                  {dx_file_delete 'ps4/track/guitar_unlit.scene_ps4'}
               )
            }
            {dx_file_delete 'settings/visuals/trackangle.dta'}
            {if_else {$this in_song}
               {$this show_state dxState_InGameWarn}
               RB4DXTRK_DATA
            }
            {$this trigger_select_effects}
         )
         ('Custom Track Angle: OFF'
            {$this show_state dxState_TrackAngleSelector}
            {$this trigger_select_effects}
         )
         (('Flames: OFF' 'Flames: ON')
            {set $noflames {! $noflames}}
            {if_else $noflames
               {do
                  MOVE_NOFLAMES_FILES_ON
                  {write_file 'data:/GoldHen/RB4DX-1.08/settings/visuals/noflames.dta' ()}
               }
               {do
                  MOVE_NOFLAMES_FILES_OFF
                  {dx_file_delete 'settings/visuals/noflames.dta'}
               }
            }
            {if_else {$this in_song}
               {$this show_state dxState_InGameWarn}
               RB4DXTRK_DATA
            }
            {$this trigger_select_effects}
         )
         (('Track Antialiasing: OFF' 'Track Antialiasing: ON')
            {set $noaa {! $noaa}}
            {if_else $noaa
               {write_file 'data:/GoldHen/RB4DX-1.08/ps4/shared/scene/antialias.scene_ps4' ()}
               {do
                  {dx_file_delete 'ps4/shared/scene/antialias.scene_ps4'}
                  {dx_emu_file_delete 'ps4/shared/scene' 'antialias.scene_ps4'}
               }
            }
            {if_else {$this in_song}
               {$this show_state dxState_InGameWarn}
               RB4DXTRK_DATA
            }
            {$this trigger_select_effects}
         )
         (('Hide Mic Warning: ON' 'Hide Mic Warning: OFF')
            {set $micwarn {! $micwarn}}
            {if_else $micwarn
               {do
                  {dx_file_rename 'ps4/ui/game/hud1.scene_ps4' 'ps4/ui/game/hud.scene_ps4'}
                  {write_file 'data:/GoldHen/RB4DX-1.08/settings/visuals/micwarn.dta' ()}
               }
               {do
                  {dx_file_rename 'ps4/ui/game/hud.scene_ps4' 'ps4/ui/game/hud1.scene_ps4'}
                  {dx_file_delete 'settings/visuals/micwarn.dta'}
               }
            }
            {if_else {$this in_song}
               {$this show_state dxState_InGameWarn}
               RB4DXTRK_DATA
            }
            {$this trigger_select_effects}
         )
         ('Track Curvature'
            {$this show_state dxState_TrackCurve}
            {$this trigger_select_effects}
         )
         ('Highway Length'
            {$this show_state dxState_HighwayLength}
            {$this trigger_select_effects}
         )
         ('Track Theme'
            {$this show_state dxState_TrackTheme}
            {$this trigger_select_effects}
         )
      }
   ) 
   (on_cancel 
      {set $dx_cur_state FALSE}
      {$this show_state dxState_DeluxeSettings}
      {$this trigger_cancel_effects}
   )
)

;Track Angle Type Selector
(dxState_TrackAngleSelector
   (view confirm_action)
   (enter
      {$this setup_confirm_actions 'Which Track Angle do you want?' ('Rock Revolution' 'Rock Band 2' 'Rock Band 3')}
   )
   (SELECT_MSG
      {local $list_data {($objId UIListCom) get_data_in_column token}}  
      {switch $list_data 
         ('Rock Revolution'
            {set $trackangletype rr}
            {$this show_state dxState_AnglesWarn}
         )
         ('Rock Band 2'
            {set $trackangletype rb2}
            {$this show_state dxState_AnglesWarn}
         )
         ('Rock Band 3'
            {set $trackangletype rb3}
            {$this show_state dxState_AnglesWarn}
         )
      }
      {$this trigger_select_effects}
   )
   (on_cancel 
      {$this show_state dxState_TrkSettings}
      {$this trigger_cancel_effects}
   )
)

;Extras
#define RB4DXEXTRA_DATA
(
   ; {if_else {$this in_song}
   ;    {$this setup_confirm_actions a 
   ;       (
   ;          ;{if_else $faststart 'Fast Start: ON' 'Fast Start: OFF'}
   ;          {if_else $notifycolored 'Plugin Notification Icon: Colored' 'Plugin Notification Icon: B/W'}
   ;       )
   ;    }
   ;    {$this setup_confirm_actions a 
   ;       (
   ;          ;'Screensaver'
   ;          ;{if_else $faststart 'Fast Start: ON' 'Fast Start: OFF'}
   ;          {if_else $notifycolored 'Plugin Notification Icon: Colored' 'Plugin Notification Icon: B/W'}
   ;       )
   ;    }
   ; }

   ;jank solution to 1.08 overshell not running dta in the list, make it a huge cond statement instead
   ;TODO pls make this better...
   {if_else $notifycolored
      {$this setup_confirm_actions a ('Plugin Notification Icon: Colored')}
      {$this setup_confirm_actions a ('Plugin Notification Icon: B/W')}
   }
)
(dxState_Extras
   (view AUTO_LIST_VIEW)
   (enter
      {set $faststart {file_exists 'data:/GoldHen/RB4DX-1.08/ps4/ui/startup/mnemonix.bk2'}}
      RB4DXEXTRA_DATA
   ) 
   (SELECT_MSG 
      {local $list_data {($objId UIListCom) get_data_in_column token}}  
      {switch $list_data
         ('Screensaver'
            {$this show_state dxState_ScreensaverWarn}
            {$this trigger_select_effects})
         (('Fast Start: ON' 'Fast Start: OFF')
            {set $faststart {! $faststart}}
            {if_else $faststart
               {do
                  {dx_file_rename 'ps4/ui/startup/mnemonix1.bk2' 'ps4/ui/startup/mnemonix.bk2'}
                  {write_file 'data:/GoldHen/RB4DX-1.08/settings/visuals/faststart.dta' ()}
               }
               {do
                  {dx_file_rename 'ps4/ui/startup/mnemonix.bk2' 'ps4/ui/startup/mnemonix1.bk2'}
                  {dx_file_delete 'settings/visuals/faststart.dta'}
               }
            }
            RB4DXEXTRA_DATA
            {$this trigger_select_effects}
         )
         (('Plugin Notification Icon: Colored' 'Plugin Notification Icon: B/W')
            {set $notifycolored {! $notifycolored}}
            {if_else $notifycolored
               {write_file 'data:/GoldHen/RB4DX-1.08/plugin/notifycolored.ini' ()}
               {do
                  {dx_file_delete "plugin/notifycolored.ini"}
                  {dx_emu_file_delete "plugin" "notifycolored.ini"}
               }
            }
            RB4DXEXTRA_DATA
            {$this trigger_select_effects}
         )
      }
   ) 
   (on_cancel 
      {$this show_state dxState_DeluxeSettings}
      {$this trigger_cancel_effects}
   )
)

#include dx_track_theme_states.dta
#include dx_slider_states.dta
#include dx_warn_states.dta
#include dx_controller_states.dta